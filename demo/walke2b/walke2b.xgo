import (
	"github.com/goplus/dql/ts"
	_ "github.com/qiniu/x/stream/zip"
)

func dumpMembers(decl ts.NodeSet, lvl int) {
	tab := "  ".repeat(lvl)
	for m in decl.members.nodes.* {
		switch m.$kind {
		case ts.KindPropertySignature:
			mdecl := m.asPropertySignatureDeclaration
			echo tab, mdecl.$name, mdecl.postfixToken.$kind == ts.KindQuestionToken
			typ := mdecl.type
			switch tkind := typ.$kind.(ts.Kind); tkind {
			case ts.KindAnyKeyword, ts.KindUnknownKeyword, ts.KindNumberKeyword,
				ts.KindBigIntKeyword, ts.KindObjectKeyword, ts.KindBooleanKeyword,
				ts.KindStringKeyword, ts.KindSymbolKeyword, ts.KindVoidKeyword,
				ts.KindUndefinedKeyword, ts.KindNeverKeyword, ts.KindIntrinsicKeyword:
				echo tab + "  ", ts.tokenToString(tkind)
			case ts.KindTypeLiteral:
				dumpMembers typ.asTypeLiteralNode, lvl + 1
			default:
				echo tab + "  ", tkind
			}
		}
	}
}

doc := ts.source("zip:e2b.zip#index.d.ts")

for n in doc.statements.nodes.*@($kind == ts.KindInterfaceDeclaration) {
	decl := n.asInterfaceDeclaration
	if decl.$name == "paths" {
		dumpMembers(decl, 0)
		break
	}
}
